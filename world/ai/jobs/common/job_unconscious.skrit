/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_unconscious.skrit

	Author(s)	: 	Bartosz Kijanka

	Purpose		: 	Unequip gear, pass out, wait for gaining consciousness, then
					get up and re-equip gear.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/


Go		m_Go$;
GoMind	m_Mind$;
Job		m_Job$;

Goid	m_Weapon$;
Goid	m_Shield$;


startup state STARTUP$
{
}


////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
}


////////////////////////////////////////////////////////////////////////////////
//	
event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	SetState UnequippingGear$;
}


////////////////////////////////////////////////////////////////////////////////
//	global event handler
trigger OnWorldMessage$( WE_DESTRUCTED )
{
	SetState DoneWaking$;
}


////////////////////////////////////////////////////////////////////////////////
//	UnequippingGear$
state UnequippingGear$
{
	event OnEnterState$
	{
		////////////////////////////////////////
		//	unequip gear
		Go weapon$ = m_Go$.Inventory.GetEquipped( ES_WEAPON_HAND );
		Go shield$ = m_Go$.Inventory.GetEquipped( ES_SHIELD_HAND );
	
		if( weapon$ != NULL )
		{
			m_Weapon$ = weapon$.Goid;
			m_Go$.Inventory.RCUnequip( ES_WEAPON_HAND );
		}

		if( shield$ != NULL )
		{
			m_Shield$ = shield$.Goid;
			m_Go$.Inventory.RCUnequip( ES_SHIELD_HAND );
		}

		////////////////////////////////////////
		//	loose our mind
		m_Mind$.Clear( JQ_BRAIN );

		SetState Collapsing$;
	}
}


////////////////////////////////////////////////////////////////////////////////
//	Collapsing$
state Collapsing$
{
	transition
	{
		-> Sleeping$:	OnWorldMessage( WE_ANIM_DONE );
	}

	event OnEnterState$
	{
		if( m_Go$.Aspect.AspectHandle.CurrentChore == CHORE_DIE )
		{
			SetState Sleeping$;
		}
		else
		{
			// Pick a Random animation to use
			eAnimStance stance$ = m_Go$.inventory.animstance;
			int subanim$ = m_Go$.Aspect.AspectHandle.NumSubAnimsForChoreAndStance(CHORE_DIE,stance$);
			subanim$ = Math.RandomInt(0, Math.MaxInt( 0, subanim$-1 ) );

			MCPManager.Flush(m_Go$.Goid);
			MCPManager.MakeRequest( m_Job$.Id, m_Go$.Goid, PL_DIE, subanim$ );
		}
	}
}


////////////////////////////////////////////////////////////////////////////////
//	Sleeping$
state Sleeping$
{
	transition
	{
		-> Waking$:		OnWorldMessage( WE_GAINED_CONSCIOUSNESS );
	}

	event OnEnterState$
	{
	}
}


////////////////////////////////////////////////////////////////////////////////
//	Waking$
state Waking$
{
	transition
	{
		-> DoneWaking$:	OnWorldMessage( WE_ANIM_DONE );
	}

	event OnEnterState$
	{
		int getupanim$ = m_Go$.Aspect.AspectHandle.GetSubAnimForChoreFourCC(CHORE_MISC,'gtup');

		if (getupanim$ >= 0)
		{
			eReqRetCode ret$ = MCPManager.MakeRequest( m_Job$.Id, m_Go$.Goid, PL_PLAYANIM, CHORE_MISC, getupanim$, 0 );
			if ( RequestFailed(ret$) )
			{
				SetState DoneWaking$;
				return;
			}
		}
		else
		{
			SetState DoneWaking$;
		}
	}
}


////////////////////////////////////////////////////////////////////////////////
//	DoneWaking$
state DoneWaking$
{
	event OnEnterState$
	{
		m_Mind$.SDoJob( MakeJobReq( JAT_BRAIN, JQ_BRAIN, QP_CLEAR, JO_REFLEX ));
		SetState RequippingGear$;
	}
}


////////////////////////////////////////////////////////////////////////////////
//	RequippingGear$
state RequippingGear$
{
	event OnEnterState$
	{
		if( IsAlive( m_Go$.Aspect.LifeState ) )
		{
			if( m_Weapon$.Go != NULL )
			{
				m_Go$.Inventory.RCEquip( ES_WEAPON_HAND, m_Weapon$ );
			}
			else
			{
				m_Go$.Inventory.RCEquip( ES_WEAPON_HAND, Goid.NoneGoid );
			}

			if( m_Shield$.Go != NULL )
			{
				m_Go$.Inventory.RCEquip( ES_SHIELD_HAND, m_Shield$ );
			}

			m_Job$.MarkForDeletion( true );
		}
	}
}
