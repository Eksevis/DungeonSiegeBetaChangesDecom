/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	brain_item.skrit

	Author(s)	: 	Eric Tams

	Purpose		:	This brain is for inteligent items which cast offensive 
					spells at nearby enemies.

	(C)opyright 2001 Gas Powered Games, Inc.

  ============================================================================
*/


Go		m_Go$;
GoMind	m_Mind$;
Job		m_Job$;
Go		m_Spell$;
Go		m_Target$;

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Spell$ 	= job$.Go.Inventory.SelectedItem;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	SetState Thinking$;
}

////////////////////////////////////////////////////////////////////////////////
//	
state Thinking$
{
	transition
	{
		-> Thinking$:		OnWorldMessage( WE_JOB_TIMER_DONE );
	}
	event OnEnterState$
	{
		if( m_Mind$.EnemiesVisible() )
		{				
			if( m_Go$.Aspect.CurrentMana < m_Spell$.Magic.ManaCost )
			{
				SetState Exiting$;
			}
			else
			{
				m_Target$ = m_Mind$.GetClosestEnemy();
				m_Spell$.Magic.SCast( m_Target$ );
			}
		}
		m_Job$.SetTimer( m_Go$.Attack.ReloadDelay );
	}
}

////////////////////////////////////////////////////////////////////////////////
//	
state Exiting$
{
	event OnEnterState$
	{
		MCPManager.Flush(m_Go$.Goid);
		m_Mind$.Clear();
		m_Job$.MarkForDeletion( true );
		//GoDb.SMarkForDeletion( m_Go$.goid );
	}
}

