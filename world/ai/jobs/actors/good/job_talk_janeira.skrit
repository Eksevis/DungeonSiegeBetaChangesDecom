/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_janeira.skrit

	Author(s)	: 	David Tomandl, Eric Tams, Chad Queen, Bartosz Kijanka

	Purpose		: 	Custom talk job for Janeira, another multiplayer world quest gal.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on coffee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			StartFidgetIfRequired$();
		}
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE ) =
		{
			StartFidgetIfRequired$();
		}
		
	}

	event OnEnterState$
	{
		#only( game )
		[[
			if( GameAuditor.GetDb.GetBool( "d_0x04e003ff" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "zconversation_directions" );
				GameAuditor.GetDb.SetBool( "d_0x04e003ff", false );
			}
			else if ( GameAuditor.GetDb.GetBool( "turn_world_red" ))
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_janeira_red" );
			}
			else if ( Victory.IsQuestCompleted( "quest_townstones" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_janeira_all_complete" );
			}
			else if ( Goid.InValidGoid == UIPartyManager.DoesActivePartyHaveTemplate( "ring_utraean", m_Target$ ))
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_janeira" );
			}
			else if ( Victory.IsQuestActive( "goto_quillrabe" )) 
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_janeira_goto" );
			}
			else if ( Goid.InValidGoid == UIPartyManager.DoesActivePartyHaveTemplate( "stone_glb_lang_01", m_Target$ ))
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_janeira_ring" );
				if ( !GameAuditor.GetDb.GetBool( "janeira_first_conversation" ) )
				{
					GameAuditor.GetDb.SetBool( "janeira_first_conversation", true );
				}					
				else if ( !GameAuditor.GetDb.GetBool( "janeira_gave_reward" ) )
				{
					GameAuditor.GetDb.SetBool( "janeira_gave_reward", true );
					// Give the reward during second conversation only
					SiegePos SpawnPos$;
					GoCloneReq cloneReq$ = MakeGoCloneReq( "stone_glb_lang_01" );
					
					SpawnPos$ = m_Target$.go.placement.position;
					cloneReq$.StartingPos	= SpawnPos$;
				    
					If( m_Target$.Go.Hasmind )
					{
						if( AIQuery.FindSpotRelativeToSource( m_Target$.Go, 1, 1.5, 2.0, m_Target$.Go.Mind.TempPos1 ) )
				   		{
							cloneReq$.StartingPos = m_Target$.Go.Mind.TempPos1;
				   		}
				   	}	
					cloneReq$.SnapToTerrain = true;
					GoDb.SCloneGo( cloneReq$ );
				}
			}
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_janeira_stone" );
			}
			StartFidgetIfRequired$();
			m_Go$.GetConversation.RSActivateDialogue();
		]]
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
