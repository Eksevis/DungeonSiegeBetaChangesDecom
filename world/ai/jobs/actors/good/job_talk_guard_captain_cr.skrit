/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_guard_captain_cr.skrit

	Author(s)	: 	David Tomandl, Eric Tams, Chad Queen, Bartosz Kijanka

	Purpose		: 	Custom talk job for the guard captain of Crystwind.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on coffee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			StartFidgetIfRequired$();
		}
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE ) =
		{
			StartFidgetIfRequired$();
		}
		
	}

	event OnEnterState$
	{
		#only( game )
		[[
			if ( GameAuditor.GetDb.GetBool( "d_0x00c0036e" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "zconversation_directions" );
				GameAuditor.GetDb.SetBool( "d_0x00c0036e", false );
			}
			else if	(
						( Victory.GetQuestOrder( "quest_zabar_hovart" ) == 4 ) && 
						( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "stone_glb_gavel_01", m_Target$ ) )
					)
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_captain_hovart_gavel" );
				// only give the reward in the second conversation
				if ( !GameAuditor.GetDb.GetBool( "cr_guard_captain_conversation_1" ))
				{
					GameAuditor.GetDb.SetBool( "cr_guard_captain_conversation_1", true);
				}
				else if ( !GameAuditor.GetDb.GetBool( "got_reward_for_hovart_quest" ))
				{
					GameAuditor.GetDb.SetBool( "got_reward_for_hovart_quest", true);
					
					// Take (delete) the gavel
					goid temp_gavel$ = UIPartyManager.DoesActivePartyHaveTemplate( "stone_glb_gavel_01", m_Target$ );
					temp_gavel$.Go.Parent.Mind.SDoJob( MakeJobReq( JAT_DROP, JQ_ACTION, QP_FRONT, AO_REFLEX, temp_gavel$ ) );
					PostWorldMessage( WE_REQ_DELETE, temp_gavel$, temp_gavel$, .1 );
					
					// Give the reward
					SiegePos SpawnPos$;
					GoCloneReq cloneReq$ = MakeGoCloneReq( "book_glb_placeholder" );
					
					SpawnPos$ = m_Target$.go.placement.position;
					cloneReq$.StartingPos	= SpawnPos$;
				    
					If( m_Target$.Go.Hasmind )
					{
						if( AIQuery.FindSpotRelativeToSource( m_Target$.Go, .25, .5, 2.0, m_Target$.Go.Mind.TempPos1 ) )
				   		{
				   			cloneReq$.StartingPos = m_Target$.Go.Mind.TempPos1;
				   		}
				   	}	
					cloneReq$.SnapToTerrain = true;
			        GoDb.SCloneGo( cloneReq$ );
					
				}
			} 
			else if	(
						( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "bd_un_ba_f_g_skeleton_captain", m_Target$ ) ) &&
						( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "stone_glb_gavel_01", m_Target$ ) ) &&
						( !GameAuditor.GetDb.GetBool( "cr_guard_captain_conversation_1" ) )
					)
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_captain_hovart_gavel" );
				// set them up to receive the reward in the second conversation
				GameAuditor.GetDb.SetBool( "cr_guard_captain_conversation_1", true);
				// give them the quest, and put them at stage 4
				Victory.RSActivateQuest( "quest_zabar_hovart", 0 );	// give quest, and 
				Victory.RSActivateQuest( "quest_zabar_hovart", 1 );	// ...
				Victory.RSActivateQuest( "quest_zabar_hovart", 2 );	// ...
				Victory.RSActivateQuest( "quest_zabar_hovart", 3 );	// ...
				Victory.RSActivateQuest( "quest_zabar_hovart", 4 );	// update quest to order 4
			} 
			else if ( Victory.IsQuestActive( "quest_zabar_hovart" ))
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_captain_hovart" );
			}
			else if ( GameAuditor.GetDb.GetBool( "turn_world_red" ) ) {
				m_Go$.GetConversation.RSSelectConversation( "conversation_captain_red" );
			} 
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_captain" );
			}
		]]
		StartFidgetIfRequired$();
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
